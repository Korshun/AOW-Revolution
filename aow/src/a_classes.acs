function void InitClassData(void)
{
    AddClass("Light Assault");
    AddClass("Heavy Assault");
    AddClass("Engineer");
    AddClass("Medic");
    AddClass("Tiberium Harvester");
    AddClass("Mech Pilot");
	AddClass("Stealth Trooper");
	AddClass("Sniper");
}

// Structure
struct AOWClass
{
	str name;
};

#define MAX_CLASSES 8

int NumClasses = 0;
AOWClass Classes[MAX_CLASSES];

function void AddClass(str name)
{
	if (NumClasses == MAX_CLASSES)
		LimitError("MAX_CLASSES");
	
	Classes[NumClasses].name = name;
	NumClasses++;
}

// Accessors
function str ClassName(int class) {	return Classes[class].name; }

// Functions






// ZDOOMVER: make this array local
int ClassHids[MAX_CLASSES];

script aow_classmenu (void) CLIENTSIDE
{
	if (PlayerNumber() != ConsolePlayerNumber())
		terminate;

	int cursor = 0;
	
	for (int i = 0; i < MAX_CLASSES; i++)
		ClassHids[i] = NewHid();

	while (true)
	{
		if (KeyPressed(BT_BACK))
			cursor++;
		if (KeyPressed(BT_FORWARD))
			cursor--;
		
		cursor = mod(cursor, NumClasses);
		
		if (KeyPressedAny(BT_USE | BT_ATTACK))
		{
			// Reliable puke.
			while (!ConsolePlayerHasClass())
			{
				RequestScriptPuke(aow_pickclass, cursor, 0, 0);
				Delay(10);
			}
			terminate;
		}
	
		int x = 320.0;
		int y = 50.0;
		
		HudResetState();
		HudSetStayTime(0.03);
		HudSetFont("BIGFONT");
		
		for (int class = 0; class < NumClasses; class++)
		{
			HudSetPoint(x, y);
			if (cursor == class)
				HudSetTextColor(CR_GOLD);
			else
				HudSetTextColor(CR_GREEN);
			HudDrawText(ClassHids[class], ClassName(class));
			y += 20.0;
		}
		
		Delay(1);
	}
}
		
script aow_pickclass (int class) NET
{
	int player = PlayerNumber();
	
	// Validate player
	if (!PlayerInGame(player) || PlayerHasClass(player) || !IsAlive())
		terminate;
		
	// Validate class
	if (class < 0 || class > NumClasses)
		terminate;
	
	SetPlayerClass(player, class);
	print(s:"Selected class: ", s:ClassName(class));
	DeployPlayer(player);
}