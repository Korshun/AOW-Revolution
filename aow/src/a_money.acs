function int PlayerMoney(int player)
{
	if (IsServer())
		return Players[player].money;
		
	int money1 = CheckActorInventory(PlayerTid(player), "MoneyClientside1");
	int money2 = CheckActorInventory(PlayerTid(player), "MoneyClientside2");
	return (money2<<15) | money1;
}

function void SetPlayerMoney(int player, int money)
{
	Players[player].money = max(0, money);
}

function void GiveMoney(int player, int money)
{
	SetPlayerMoney(player, PlayerMoney(player) + money);
}

script aow_syncmoney OPEN
{
	while (true)
	{
		for (int player = 0; player < MAX_PLAYERS; player++)
		{
			if (!PlayerInGame(player))
				continue;
				
			// Credit flow
			if (Timer() % 35 == 0)
				GiveMoney(player, GetCVar("aowr_credflow"));
		
			int money = PlayerMoney(player);
			SetActorInventory(PlayerTid(player), "Money", money);
			SetActorInventory(PlayerTid(player), "MoneyClientside1", money & 0x7fff);
			SetActorInventory(Playertid(player), "MoneyClientside2", money>>15);
		}
		
		Delay(1);
	}
}

script aow_givemoney (int money)
{
	GiveMoney(PlayerNumber(), money);
}
