struct AOWPlayer
{
	int money;
	
	bool hasClass;
	int class;
};

AOWPlayer Players[MAX_PLAYERS];

function int PlayerTid(int player)
{
	return 1000 + player;
}

function int ConsolePlayerTid(void)
{
	return PlayerTid(ConsolePlayerNumber());
}

function int GetPlayerTeam(int player)
{
	if (!PlayerInGame(player))
		return TEAM_NONE;
	if (CheckActorInventory(PlayerTid(player), "IsRed"))
		return TEAM_RED;
	return TEAM_BLUE;
}

function int ConsolePlayerTeam(void)
{
	return GetPlayerTeam(ConsolePlayerNumber());
}

function void SetActivatorToPlayer(int player)
{
	int count = ThingCount(T_NONE, PlayerTid(player));
	if (count == 0)
		ProgramError(StrParam(s:"SetActivatorToPlayer: Player ", d:player, s:" has no tid"));
	else if (count > 1)
		ProgramError(StrParam(s:"SetActivatorToPlayer: Player ", d:player, s:"'s tid is taken by ", d:count, s:"actors"));

	SetActivator(PlayerTid(player));
}

function void InitPlayer(void)
{
	Thing_ChangeTid(0, PlayerTid(PlayerNumber()));
	SetInventory("IsRed", PlayerTeam() == TEAM_RED);
	SetInventory("AOWReloadGiver", 1);
	SetInventory("AOWReloadTaker", 1);
	SetInventory("AOWGrenadeGiver", 1);
	SetInventory("AOWGrenadeTaker", 1);
	
	int player = PlayerNumber();
	Players[player].hasClass = false;
	
	TeleportToSpawnRoom();
}

script aow_enter ENTER
{
	InitPlayer();
	SetPlayerMoney(PlayerNumber(), GetCVar("aowr_startcredits"));
}

script aow_respawn RESPAWN
{
	InitPlayer();
}

script aow_enter_clientside ENTER CLIENTSIDE
{
	InitPlayerClientside();
}

script aow_respawn_clientside RESPAWN CLIENTSIDE
{
	InitPlayerClientside();
}

function void InitPlayerClientside(void)
{
	ACS_ExecuteWithResult(aow_playerhud);
}

function bool ConsolePlayerInGame(void)
{
	if (!IsClient())
	{
		ProgramError("ConsolePlayerInGame() called serverside, returning false.");
		return false;
	}
	
	return PlayerInGame(ConsolePlayerNumber());
}

script aow_playerhud (void)
{
	int hid = NewHid();
	
	while (ConsolePlayerInGame())
	{
		HudResetState();
		HudSetStayTime(0.03);
		HudSetTextOriginX(HUD_TEXTORIGIN_LEFT);
		int xscale = FixedDiv(GetAspectRatio(), ASPECT_4_3);
		HudSetPoint(HudRight() - 202 * xscale, HudBottom() - 88.0);
		HudDrawText(hid, StrParam(s:"\cjCredits: \cd$", d:PlayerMoney(ConsolePlayerNumber())));
		
		Delay(1);
	}
}
