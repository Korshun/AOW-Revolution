Actor SandbagWall 
{
	Health 650
	Radius 40
	Height 64
	Mass 0x7FFFFFFF
	
	Scale 0.35
	RenderStyle Translucent
	Alpha 0.0
	
	+NOBLOOD
	+ISMONSTER // Needed for A_RemoveChildren to work
	
	DamageFactor "Laser", 1.25
	DamageFactor "Laser", 1.25
	DamageFactor "Disarm", 0.0
	DamageFactor "Flash", 0.0
	DamageFactor "RazorWire", 0.0
	
	States
	{
	Spawn:
		NULL A 0
		BAGG AAAAAAAAAA 3 A_FadeIn(0.1)
		NULL A 0 A_FadeIn(0.1) // what if it fades to 0.9999999?
		BAGG A 70
		NULL A 0 A_SetSolid
		NULL A 0 A_SetShootable
		BAGG A 35 A_Gravity
		NULL A 0 A_SpawnItemEx("SandbagWallSideBlocker",  0,  32, 0,  0, 0, 0,  0, SXF_SETMASTER | SXF_NOCHECKPOSITION)
		BAGG A -1 A_SpawnItemEx("SandbagWallSideBlocker", 0, -32, 0,  0, 0, 0,  0, SXF_SETMASTER | SXF_NOCHECKPOSITION)
		Stop
	Death:
		NULL A 0 A_RemoveChildren(true)
		NULL A 0 A_NoBlocking
		NULL A 0 A_SpawnItemEx("Explosion", 0, 0, 32)
		NULL A 0 A_PlaySound("weapons/rocklx")
		TNT1 A 1
		Stop
	Death.Deconstruction:
		"----" A 0 A_NoBlocking
		"----" A 0 A_RemoveChildren(true)
		"----" A 2
		"----" A 2 A_Fadeout(0.1) 
		Wait
	}
}

Actor SandbagWallSideBlocker 
{
	Radius 16
	Height 64
	Mass 0x7FFFFFFF
	
	+SOLID
	+ISMONSTER // Needed for A_RemoveChildren to work
	
	States
	{
	Spawn:
		TNT1 A -1
		Stop
	}
}