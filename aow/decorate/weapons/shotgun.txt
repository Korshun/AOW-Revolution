Actor BoomstickSpawner replaces Shotgun
{
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItem("Boomstick")
		stop
	}
}

Actor Boomstick : AOWWeapon 21080
{
	Tag "Shotgun"
	Inventory.PickupMessage "Shotgun"
	Obituary "%o was smacked down by %k's shotgun."
	Weapon.PreferredSkin "MarineShotgun"
	
	Inventory.RespawnTics 2100
	
	Weapon.Kickback 80
	
	Weapon.AmmoUse 1
	Weapon.AmmoGive 10
	Weapon.AmmoType "ShotgunAmmo"
	
	AttackSound "weapons/shotgf"
	
	States
	{
	Ready:
		SHTG A 1 A_WeaponReady
		Wait
	Deselect:
		SHTG A 1 A_Lower
		Wait
	Select:
		SHTG A 1 A_Raise
		Wait
	Fire:
		SHTG A 3
		NULL A 0 A_GunFlash
		NULL A 0 ACS_ExecuteWithResult(aow_traceroffset, 0, 0)
		NULL A 0 BRIGHT A_FireBullets(5.5, 5.5, 2, 12, "BulletPuff", FBF_NORANDOM, 350)
		SHTG A 2 BRIGHT A_FireBullets(5.5, 5.5, 6, 8, "BulletPuff", FBF_USEAMMO | FBF_NORANDOM, 1000)
		SHTG A 3 BRIGHT
		SHTG BC 4
		SHTG D 4 A_SpawnItemEx("ShellCasing", 0, 0, ACS_ExecuteWithResult(aow_casingheight, 32), frandom(1, 3), frandom(0, 1), 0, frandom(45, 60), 128)
		SHTG CB 4
		SHTG A 3
		SHTG A 7 A_ReFire
		Goto Ready
	Flash:
		SHTF A 2 BRIGHT A_Light1
		SHTF B 3 BRIGHT A_Light2
		NULL A 0 A_Light0
		Stop
	Spawn:
		SHOT A -1
		Stop
	}
}

Actor ShotgunAmmo : Ammo
{
	Inventory.Amount 5
	Inventory.MaxAmount 50
}
