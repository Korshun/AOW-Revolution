// Shock rifle
Actor ShockRifle : AOWWeapon 21068
{
	Tag "Shock Rifle"
	Inventory.PickupMessage "Shock Rifle"
	Obituary "%o felt %k's surge."
	Weapon.PreferredSkin "MarineShockRifle"

	Weapon.AmmoUse 1
	Weapon.AmmoGive 80
	Weapon.AmmoType "ShockRifleCharge"
	
	Decal "VoltageScorch"
	Weapon.UpSound "VOLTUP"
	AttackSound "VOLTFIRE"
	
	States
	{
	Ready:
		VOLT AAABBBCCCDDDCCCBBB 1 A_WeaponReady
		Loop
	Deselect:
		VOLT A 1 A_Lower
		Loop
	Select:
		VOLT A 1 A_Raise
		Loop
	Fire:
		VOLF ABC 3 BRIGHT
		NULL A 0 A_ReFire
		VOLF CBA 3 BRIGHT
		Goto Ready
	Hold:
		VOLF E 1 BRIGHT A_FireBullets(3.2, 3.2, 3, 5, "VoltagePuff", FBF_NORANDOM | FBF_USEAMMO, 1100)
		VOLF D 2 BRIGHT
		NULL A 0 A_ReFire
		VOLF CBA 3 BRIGHT
		Goto Ready
	Spawn:
		VOLF X -1
		Stop
	}
}

Actor ShockRifleCharge : Ammo
{
	Inventory.Amount 20
	Inventory.MaxAmount 200
}

Actor VoltagePuff : Effect
{
	DamageType "Laser"

	+PUFFONACTORS 
	+ALWAYSPUFF
	
	RenderStyle Add
	Alpha 0.5	
	Scale 1.2
	
	States
	{
	Spawn:
		VOLX A 2 BRIGHT
		NULL A 0 A_SpawnItemEx("VoltageDecay", 0, 0, 0,  0, 0, 0,  0, 0, 192)
		VOLX BCDEFEDCB 2 BRIGHT
		Stop
	}
}

Actor VoltageDecay : Effect
{
	RenderStyle Add
	Alpha 1
	Scale 1.5
	
	States
	{
	Spawn:
		NULL AA 0 A_PlaySound("VOLTDECA")
		NULL A 0 A_Jump(128, "AltSpawn")
	Spawn1:
		VOLX AABBCCDDEEFFEECCA 2 BRIGHT A_SpawnItemEx("VoltageSpark", 0, 0, 0,  frandom(-2, 2), frandom(-2, 2), frandom(0, 2),  frandom(-180, 180), 0, 64)
		Stop
	AltSpawn:
		VOLX AACCBBDDFFEECCB 2 BRIGHT A_SpawnItemEx("VoltageSpark", 0, 0, 0,  frandom(-2, 2), frandom(-2, 2), frandom(0, 2),  frandom(-180, 180), 0, 64)
		Stop
	}
}

Actor VoltageSpark : Effect
{
	RenderStyle Add
	Alpha 0.8
	Scale 0.5
	
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_Jump(96, "Hidden")
		VOLX ACBDFECBA 2 BRIGHT
		Stop
	}
}
