Actor HandgunSpawner replaces Pistol
{
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItem("Handgun")
		stop
	}
}

Actor Handgun : AOWWeapon 21041
{
	Tag "Handgun"
	Inventory.PickupMessage "Handgun"
	Obituary "%o was capped by %k's handgun."
	Weapon.PreferredSkin "MarinePistol"

	+WEAPON.WIMPY_WEAPON
	+WEAPON.NOAUTOFIRE
	
	Weapon.AmmoUse 1
	Weapon.AmmoGive 40
	Weapon.AmmoType "NewClipAmmo"
	
	Weapon.UpSound "PISTUP"
	AttackSound "PISTFIRE"
	
	States
	{
	Ready:
		NULL A 0 A_JumpIfInventory("FragGrenadeAmmo", 1, 1)
		Goto Ready+2
		NULL A 0 A_JumpIfInventory("AOWGrenade", 1, "FragGrenade")
		PISG A 1 A_WeaponReady
		Goto Ready
	Deselect:
		NULL A 0 A_Lower
		PISG A 1 A_Lower
		Loop
	Select:
		NULL A 0 A_Raise
		PISG A 1 A_Raise
		Loop
	Fire:
		NULL A 0 A_SpawnItemEx("PistolCasing", 8, 8, CallACS("aow_casingheight", 32), frandom(1, 3), frandom(0, 1), frandom(1, 3), frandom(45, 60), 128)
		NULL A 0 ACS_NamedExecuteWithResult("aow_traceroffset", 0, 0)
		NULL A 0 A_FireBullets(2.0, 2.0, -1, 8, "BulletPuff", FBF_USEAMMO | FBF_NORANDOM) // range: 1536
		PISG B 4 A_GunFlash
		PISG CC 1 A_WeaponReady
		PISG BBB 1 A_WeaponReady
		NULL A 0 A_ReFire
		Goto Ready
	Flash:
		PISF A 1 BRIGHT A_Light2
		PISF A 1 BRIGHT A_Light1
		Goto LightDone
	FragGrenade:
		PISG A 1 offset (4,34)
		PISG A 1 offset (16,49)
		PISG A 1 offset (32,79)
		PISG A 1 offset (48,125)
        NULL A 0 offset (1,33)
		Goto Generic_FragGrenade
	GrenadeReturn:
		NULL A 0 A_PlaySound("PISTUP", CHAN_WEAPON)
		PISG A 1 offset (120,139)
		PISG A 1 offset (100,112)
		PISG A 1 offset (80,89)
		PISG A 1 offset (60,70)
		PISG A 1 offset (40,55)
		PISG A 1 offset (20,44)
		PISG A 1 offset (10,37)
		PISG A 1 offset (0,0)
		Goto Ready	
	Spawn:
		PIST A -1
		Stop
	}
}

Actor NewClipAmmo : Ammo
{
	Inventory.Amount 30
	Inventory.MaxAmount 400
}
