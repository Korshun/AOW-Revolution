Actor PlasmaCannon : AOWWeapon
{
	Tag "Plasma Cannon"
	Inventory.PickupMessage "Plasma Cannon"
	Obituary "%o was demolished by %k's artillery cannon."
	Weapon.PreferredSkin "MarineBFG"

	Weapon.AmmoUse 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "PlasmaCannonAmmo"
	Weapon.AmmoType2 "PlasmaCannonAmmo"
	DropItem "NewBackPack"

	Weapon.Kickback 100
	
	Weapon.UpSound "PLCUP"
	
	States
	{
	Ready:
		NULL A 0 A_JumpIfInventory("IsRed", 1, "ReadyRed")
	ReadyBlue:
		PLCG ABCD 3 A_WeaponReady
		Loop
	ReadyRed:
		PC2G ABCD 3 A_WeaponReady
		Loop
	Deselect:
		NULL AA 0 A_JumpIfInventory("IsRed", 1, "DeselectRed")
	DeselectBlue:
		PLCG AAABBBCCCDDD 1 A_Lower
		Loop
	DeselectRed:
		PC2G AAABBBCCCDDD 1 A_Lower
		Loop
	Select:
		NULL A 0 A_JumpIfInventory("IsRed", 1, "SelectRed")
	SelectBlue:
		PLCG AAABBBCCCDDD 1 A_Raise
		Loop
	SelectRed:
		PC2G AAABBBCCCDDD 1 A_Raise
		Loop
	Fire:
		NULL A 0 A_JumpIfInventory("IsRed", 1, "FireRed")
	FireBlue:
		PLCG A 8 A_PlayWeaponSound("PLCFIRE")
		PLCG EFG 6 BRIGHT
		NULL A 0 A_GunFlash
		NULL A 0 A_LightInverse
		PLCG H 2 BRIGHT A_FireCustomMissile("PlasmaCannonBallBlue")
		PLCG H 3 BRIGHT A_Light2
		PLCG I 2 BRIGHT A_Light1
		PLCG I 3 BRIGHT A_Light0
		PLCG JKL 5 BRIGHT
		PLCG EF 3
		PLCG G 5 A_ReFire
		Goto Ready
	FireRed:
		PC2G A 8 A_PlayWeaponSound("PLCFIRE")
		PC2G EFG 6 BRIGHT
		NULL A 0 A_GunFlash
		NULL A 0 A_LightInverse
		PC2G H 2 BRIGHT A_FireCustomMissile("PlasmaCannonBallRed")
		PC2G H 3 BRIGHT A_Light2
		PC2G I 2 BRIGHT A_Light1
		PC2G I 3 BRIGHT A_Light0
		PC2G JKL 5 BRIGHT
		PC2G EF 3
		PC2G G 5 A_ReFire
		Goto Ready

	AltFire:
		NULL A 0 A_JumpIfInventory("IsRed", 1, "AltFireRed")
	AltFireBlue:
		PLCG A 5 A_PlayWeaponSound("PLCALTF")
		PLCG EFG 5 BRIGHT
		NULL A 0 A_GunFlash
		NULL A 0 A_LightInverse
		PLCG H 2 BRIGHT A_FireCustomMissile("PlasmaCannonSmallBallBlue")
		PLCG H 2 BRIGHT A_Light2
		PLCG I 2 BRIGHT A_Light1
		PLCG I 2 BRIGHT A_Light0
		PLCG JKL 4 BRIGHT
		PLCG EF 3
		PLCG G 5 A_ReFire
		Goto Ready
	AltFireRed:
		PC2G A 5 A_PlayWeaponSound("PLCALTF")
		PC2G EFG 5 BRIGHT
		NULL A 0 A_GunFlash
		NULL A 0 A_LightInverse
		PC2G H 2 BRIGHT A_FireCustomMissile("PlasmaCannonSmallBallRed")
		PC2G H 2 BRIGHT A_Light2
		PC2G I 2 BRIGHT A_Light1
		PC2G I 2 BRIGHT A_Light0
		PC2G JKL 4 BRIGHT
		PC2G EF 3
		PC2G G 5 A_ReFire
		Goto Ready
	}
}

Actor PlasmaCannonAmmo : Ammo
{
	Inventory.Amount 0
	Inventory.MaxAmount 10
}

// Big plasma ball.

Actor PlasmaCannonBall
{
	Radius 13
	Height 8
	Speed 40
	Damage (200)
	DamageType "Explosion"

	PROJECTILE
	+FORCEXYBILLBOARD
	+EXTREMEDEATH

	RenderStyle Add
	Alpha 0.75
	Scale 1.5
	
	DeathSound "PLCEXP"

	States
	{
	Spawn:
		PLS1 AABB 1 BRIGHT A_SpawnItemEx("PlasmaFlameTail", 0, 0, 0,  0, 0, 0,  0, SXF_TRANSFERTRANSLATION)
		Loop
	}
}

Actor PlasmaCannonBallRed : PlasmaCannonBall
{
	Translation "192:207=169:189", "240:246=45:47", "112:127=176:191"
	Obituary "%o was annihilated by %k's \caPlasma Cannon."
	
	States 
	{
	Death:
		PLE1 A 4
		PLE1 A 4 BRIGHT A_Explode(300, 192)
		PLE1 BBBB 2 BRIGHT A_SpawnItemEx("PlasmaFlameDecayRed", frandom(-32, 32), frandom(-32, 32), frandom(-32, 32),  0, 0, 0,  0, SXF_TRANSFERTRANSLATION)
		NULL A 0 A_SpawnItemEx("PlasmaFlameCluster", 0, 0, 0,  0, 0, 0,  0, SXF_TRANSFERTRANSLATION)
		PLE1 C 4 BRIGHT
		PLE1 C 4 BRIGHT A_Explode(30, 550, 0)
		PLE1 DEF 8 BRIGHT
		Stop
	}
}

Actor PlasmaCannonBallBlue : PlasmaCannonBall
{
	Translation "112:120=200:207", "121:127=241:247"
	Obituary "%o was annihilated by %k's \cnPlasma Cannon."
	
	States
	{
	Death:
		PLE1 A 4
		PLE1 A 4 BRIGHT A_Explode(300, 192)
		PLE1 BBBB 2 BRIGHT A_SpawnItemEx("PlasmaFlameDecayBlue", frandom(-32, 32), frandom(-32, 32), frandom(-32, 32),  0, 0, 0,  0, SXF_TRANSFERTRANSLATION)
		NULL A 0 A_SpawnItemEx("PlasmaFlameCluster", 0, 0, 0,  0, 0, 0,  0, SXF_TRANSFERTRANSLATION)
		PLE1 C 4 BRIGHT
		PLE1 C 4 BRIGHT A_Explode(30, 550, 0)
		PLE1 DEF 8 BRIGHT
		Stop
	}
}

// Small plasma ball.

Actor PlasmaCannonSmallBall : PlasmaCannonBall
{
	Radius 9
	Height 7
	Speed 45
	Damage (100)
	DamageType "Explosion"

	+SEEKERMISSILE
	-EXTREMEDEATH
	
	Scale 0.9
	
	DeathSound "PLCAEXP"
	
	States
	{
	Spawn:
		NULL A 0 A_Tracer2
		PLS1 ABA 1 BRIGHT A_SpawnItemEx("PlasmaFlameTailSmall", 0, 0, 0,  0, 0, 0,  0, SXF_TRANSFERTRANSLATION)
		NULL A 0 A_Tracer2
		PLS1 BAB 1 BRIGHT A_SpawnItemEx("PlasmaFlameTailSmall", 0, 0, 0,  0, 0, 0,  0, SXF_TRANSFERTRANSLATION)
		Loop
	Death:
		PLE1 A 3
		PLE1 A 3 BRIGHT A_Explode(128, 128)
		PLE1 B 7 BRIGHT
		PLE1 C 3 BRIGHT
		PLE1 C 3 BRIGHT A_Explode(128, 256)
		PLE1 DEF 7 BRIGHT
		Stop
	}
}

Actor PlasmaCannonSmallBallRed : PlasmaCannonSmallBall
{
	Translation "192:207=169:189", "240:246=45:47", "112:127=176:191"
	Obituary "%o couldn't escape %k's \caPlasma Cannon."
}

Actor PlasmaCannonSmallBallBlue : PlasmaCannonSmallBall
{
	Translation "112:120=200:207", "121:127=241:247"
	Obituary "%o couldn't escape %k's \cnPlasma Cannon."
}

// Plasma flame effect.

Actor PlasmaFlame : Effect
{
	Radius 13
	Height 8
	Speed 10
	Damage (0)
	
	RenderStyle Add
	Alpha 0.75
	Scale 1.2
	
	States
	{
	Spawn:
		PLFX ABCDEFG 3 BRIGHT
		PLFX FG 2 BRIGHT
		Stop
	}
}

Actor PlasmaFlameSmall : PlasmaFlame
{
	Scale 0.8
	
	States
	{
	Spawn:
		PLFX ABCDEFG 2 BRIGHT
		Stop
	}
}

// Plasma flame spawners.

Actor PlasmaFlameTail : Effect
{
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItemEx("PlasmaFlame", -8, 0, 0,  -6, frandom(-2, -3), frandom(-1, 1),  0, SXF_TRANSFERTRANSLATION)
		TNT1 A 1 A_SpawnItemEx("PlasmaFlame", -8, 0, 0,  -6, frandom(2, 3), frandom(-1, 1),  0, SXF_TRANSFERTRANSLATION)
		Stop
	}
}

Actor PlasmaFlameTailSmall : PlasmaFlameTail
{
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItemEx("PlasmaFlameSmall", -8, 0, 0,  -5, frandom(-2, -3), frandom(-1, 1),  0, SXF_TRANSFERTRANSLATION)
		TNT1 A 1 A_SpawnItemEx("PlasmaFlameSmall", -8, 0, 0,  -5, frandom(2, 3), frandom(-1, 1),  0, SXF_TRANSFERTRANSLATION)
		Stop
	}
}

Actor PlasmaFlameCluster : Effect
{
	ReactionTime 60
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItemEx("PlasmaFlame", 0, 0, 0,  frandom(-9, 9), frandom(-10, 10), frandom(-10, 10),  0, SXF_TRANSFERTRANSLATION)
		NULL A 0 A_Countdown
		Loop
	Death:
		NULL A 0
		Stop
	}
}

Actor PlasmaFlameDecayFX : Effect
{
	ReactionTime 66
	States
	{
	Spawn:
		NULL A 0
		TNT1 A 5 A_SpawnItemEx("PlasmaFlame", frandom(-16, 16), frandom(-16, 16), frandom(8, 16),  0, 0, frandom(1, 3),  0, SXF_TRANSFERTRANSLATION)
		NULL A 0 A_Countdown
		Loop
	Death:
		NULL A 0
		Stop
	}
}

// Plasma decay.

Actor PlasmaFlameDecay
{
	+NOBLOCKMAP
	+NOGRAVITY
	
	RenderStyle None
	DamageType "Fire"
	
	Obituary "%o burns in %k's firestorm."
	
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItemEx("PlasmaFlameDecayFX", 0, 0, 0,  0, 0, 0,  0, SXF_TRANSFERTRANSLATION)
		TNT1 AAAAAAAAAAAAAAAAAAAAAA 15 BRIGHT A_Explode(8, 96, 0)
		Stop
	}
}

// Separate actors needed for colored dynamic lights.
Actor PlasmaFlameDecayRed : PlasmaFlameDecay {}
Actor PlasmaFlameDecayBlue : PlasmaFlameDecay {}
