Actor SniperRifleUpgrade : Boolean {}

Actor SniperRifle : AOWWeapon 21081
{
	Tag "Sniper Rifle"
	Inventory.PickupMessage "Sniper Rifle"
	Obituary "%o found %k's bullet in %p head."
	Weapon.PreferredSkin "MarineSniper"

	+NOEXTREMEDEATH
	
	Weapon.Kickback 80
	
	Weapon.AmmoUse 1
	Weapon.AmmoGive 10
	Weapon.AmmoType "SniperAmmo"
	
	Weapon.UpSound "SNIPUP"
	AttackSound "SNIPFIRE"
	
	States
	{
	Ready:
		NULL A 0 A_JumpIfInventory("IsZoomed", 1, "ReadyZoomed")
	ReadyNormal:
		NULL A 0 A_JumpIfInventory("FragGrenadeAmmo", 1, 1)
		Goto ReadyNormal+2
		NULL A 0 A_JumpIfInventory("AOWGrenade", 1, "FragGrenade")
		SNIE A 1 A_WeaponReady
		Goto Ready
	ReadyZoomed:
		TNT1 A 1 A_WeaponReady
		Loop
	Deselect:
		NULL A 0 A_JumpIfInventory("IsZoomed", 1, "ZoomOut")
		SNIE A 1 A_Lower
		Wait
	Select:
		SNIE A 1 A_Raise
		Loop
	Fire:
		NULL A 0 A_GunFlash
		NULL A 0 A_JumpIfInventory("IsZoomed", 1, "FireZoomed")
	FireNormal:	
		NULL A 0 A_JumpIfInventory("SniperRifleUpgrade", 1, "FireNormalUpgraded")
		NULL A 0 ACS_NamedExecuteWithResult("aow_traceroffset", 8, 0)
		NULL A 0 A_FireBullets(7.2, 7.2, -1, 34, "BulletPuff", FBF_USEAMMO | FBF_NORANDOM)
		Goto Reload
	FireNormalUpgraded:
		NULL A 0 ACS_NamedExecuteWithResult("aow_traceroffset", 8, 0)
		NULL A 0 A_FireBullets(7.2, 7.2, -1, 50, "BulletPuff", FBF_USEAMMO | FBF_NORANDOM)
		Goto Reload
	FireZoomed:
		NULL A 0 A_JumpIfInventory("SniperRifleUpgrade", 1, "FireZoomedUpgraded")
		NULL A 0 ACS_NamedExecuteWithResult("aow_traceroffset", 8, 0)
		NULL A 0 A_RailAttack(50, 0, 0, none, none, RGF_SILENT, 0, "BulletPuff")
		NULL A 0 A_FireBullets(0.0, 0.0, 1, 0, "BulletPuff", FBF_USEAMMO | FBF_NORANDOM)
		Goto Reload
	FireZoomedUpgraded:
		NULL A 0 ACS_NamedExecuteWithResult("aow_traceroffset", 8, 0)
		NULL A 0 A_RailAttack(75, 0, 0, none, none, RGF_SILENT, 0, "BulletPuff")
		NULL A 0 A_FireBullets(0.0, 0.0, 1, 0, "BulletPuff", FBF_USEAMMO | FBF_NORANDOM)
		Goto Reload
	Reload:
		NULL A 0 A_JumpIfInventory("IsZoomed", 1, "ReloadZoomed")
	ReloadNormal:
		SNIF F 2 BRIGHT
		SNIE A 8
		SNIR ABCDEFGHIJ 2
		SNIR C 5 offset(0, 40) A_PlaySound("SNIPOPEN")
		SNIR C 5 offset(0, 35) A_SpawnItemEx("RifleCasing", 0, 8, CallACS("aow_casingheight", 32), frandom(1, 3), frandom(0, 1), frandom(1, 3), frandom(45, 60))
		SNIR C 10 offset(0, 33) A_PlaySound("SNIPCLOS")
		SNIR BA 3
		SNIE A 1 A_ReFire
		Goto Ready
	ReloadZoomed:
		TNT1 A 30
		TNT1 A 5 offset(0, 40) A_PlaySound("SNIPOPEN")
		TNT1 A 5 offset(0, 35) A_SpawnItemEx("RifleCasing", 0, 8, CallACS("aow_casingheight", 32), frandom(1, 3), frandom(0, 1), frandom(1, 3), frandom(45, 60))
		TNT1 A 10 offset(0, 33) A_PlaySound("SNIPCLOS")
		TNT1 A 6
		TNT1 A 1 A_ReFire
		Goto Ready
	AltFire:
		NULL A 0 A_JumpIfInventory("IsZoomed", 1, "ZoomOut")
	ZoomIn:
		NULL A 0 A_GiveInventory("IsZoomed", 1)
		NULL A 0 A_ZoomFactor(6.0)
		SNIE A 4
		SNIE A 1 offset(0, 32)
		SNIE A 1 offset(0, 40)
		SNIE A 1 offset(0, 48)
		SNIE A 1 offset(0, 56)
		SNIE A 1 offset(0, 64)
		SNIE A 1 offset(0, 72)
		SNIE A 1 offset(0, 80)
		SNIE A 1 offset(0, 88)
		SNIE A 1 offset(0, 96)
		SNIE A 1 offset(0, 104)
		SNIE A 1 offset(0, 112)
		SNIE A 1 offset(0, 120)
		SNIE A 1 offset(0, 128)
		SNIE A 1 offset(0, 160)
		Goto Ready
	ZoomOut:
		NULL A 0 A_TakeInventory("IsZoomed", 1)
		NULL A 0 A_ZoomFactor(1.0)
		SNIE A 4 offset(0, 160)
		SNIE A 1 offset(0, 128)
		SNIE A 1 offset(0, 120)
		SNIE A 1 offset(0, 112)
		SNIE A 1 offset(0, 104)
		SNIE A 1 offset(0, 96)
		SNIE A 1 offset(0, 88)
		SNIE A 1 offset(0, 80)
		SNIE A 1 offset(0, 72)
		SNIE A 1 offset(0, 64)
		SNIE A 1 offset(0, 56)
		SNIE A 1 offset(0, 48)
		SNIE A 1 offset(0, 40)
		SNIE A 1 offset(0, 32)
		Goto Ready
	FragGrenade:
	    SNIE A 1 offset (4,34)
		SNIE A 1 offset (16,49)
		SNIE A 1 offset (32,79)
		SNIE A 1 offset (48,125)
        NULL A 0 offset (1,33)
		Goto Generic_FragGrenade
	GrenadeReturn:
		NULL A 0 A_PlayWeaponSound("SNIPUP")
        SNIE A 1 offset (120,139)
		SNIE A 1 offset (100,112)
		SNIE A 1 offset (80,89)
		SNIE A 1 offset (60,70)
		SNIE A 1 offset (40,55)
		SNIE A 1 offset (20,44)
		SNIE A 1 offset (10,37)
		SNIE A 1 offset (0,0)
		Goto Ready
	Spawn:
		SNIP X -1
		Stop
	}
}

Actor SniperAmmo : Ammo
{
	Inventory.Amount 4
	Inventory.MaxAmount 40
}
