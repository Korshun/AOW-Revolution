Actor GatlingGunSpawner replaces Minigun
{
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItem("GatlingGun")
		stop
	}
}

Actor GatlingGun : AOWWeapon 21045
{
	Tag "Gatling Gun"
	Inventory.PickupMessage "Gatling Gun"
	Obituary "%o was perforated by %k's gatling gun."
	Weapon.PreferredSkin "MarineChaingun"

	Weapon.AmmoUse 1
	Weapon.AmmoUse2 2
	Weapon.AmmoGive 50
	Weapon.AmmoType "MachineGunAmmo"
	Weapon.AmmoType2 "MachineGunAmmo"
	
	Weapon.UpSound "GATLUP"
	
	States
	{
	Ready:
		NULL A 0 A_StopSound(CHAN_WEAPON)
		GATG A 1 A_WeaponReady
		Loop
	Deselect:
		NULL A 0 A_StopSound(CHAN_WEAPON)
		GATG A 1 Offset(-4,34)
		GATG A 1 Offset(-8,40)
		GATG A 1 Offset(-16,49)
		GATG A 1 Offset(-48,125)
		GATG A 1 Offset(-24,62)
		GATG A 1 Offset(-32,79)
		GATG A 1 Offset(-40,100) 
		GATG A 1 Offset(-56,154)
		NULL A 0 Offset(-1,33)
		NULL AAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		Stop
	Select:
		GATG A 1 Offset(-130,154)
		GATG A 1 Offset(-120,139)
		GATG A 1 Offset(-110,125)
		GATG A 1 Offset(-100,112)
		GATG A 1 Offset(-90,100)
		GATG A 1 Offset(-80,89)
		GATG A 1 Offset(-60,70)
		GATG A 1 Offset(-50,62)
		GATG A 1 Offset(-40,55)
		GATG A 1 Offset(-30,49)
		GATG A 1 Offset(-20,44)
		GATG A 1 Offset(-15,40)
		GATG A 1 Offset(-10,37)
		GATG A 1 Offset(-5,34)
		GATG A 1 Offset(0,0)
		NULL AAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		Goto Ready
	Fire:
		GATG B 5 A_PlaySound("GATLSUP", CHAN_5)
		GATG A 4
		GATG B 3
	Hold:
		GATG A 1 A_PlaySound("GATLLOOP", CHAN_WEAPON, 1.0, true)
		NULL A 0 A_PlaySound("GATLFIRE", CHAN_5)
		
		NULL A 0 A_Light2
		NULL A 0 A_SpawnItemEx("RifleCasing", 2, 8, ACS_ExecuteWithResult(aow_casingheight, 32), frandom(1, 3), frandom(0, 1), frandom(1, 3), frandom(45, 60))
		NULL A 0 ACS_ExecuteWithResult(aow_traceroffset, 3, 0)
		GATF A 1 BRIGHT A_FireBullets(7.2, 7.2, -1, 12, "BulletPuff", FBF_USEAMMO | FBF_NORANDOM) // range: 3072
		GATG B 1 A_Light1
		
		NULL A 0 A_Light2
		NULL A 0 A_SpawnItemEx("RifleCasing", 2, 8, ACS_ExecuteWithResult(aow_casingheight, 32), frandom(1, 3), frandom(0, 1), frandom(1, 3), frandom(45, 60))
		NULL A 0 ACS_ExecuteWithResult(aow_traceroffset, 3, 0)
		GATF B 1 BRIGHT A_FireBullets(7.2, 7.2, -1, 12, "BulletPuff", FBF_USEAMMO | FBF_NORANDOM) // range: 3072
		GATG A 1 A_Light1
	FireEnd:
		NULL A 0 A_ReFire
		NULL A 0 A_Light0
		GATG B 3 A_PlaySound("GATLSDWN", CHAN_5)
		NULL A 0 A_StopSound(CHAN_WEAPON)
		GATG A 5 A_ReFire
		GATG B 6 A_ReFire
		GATG AB 7
		Goto Ready
	AltFire:
		GATG B 5 A_PlaySound("GATLSUP", CHAN_5)
		GATG A 4
		GATG B 3
	AltHold:
		NULL A 0 A_PlaySound("GATLALTF", CHAN_WEAPON, 1.0, true)
		
		NULL A 0 A_Light2
		NULL AA 0 BRIGHT A_SpawnItemEx("RifleCasing", 2, 8, ACS_ExecuteWithResult(aow_casingheight, 32), frandom(1, 3), frandom(0, 1), frandom(1, 3), frandom(45, 60), 128)
		GATF A 2 BRIGHT A_FireBullets(2.6, 1.6, -1, 20, "ExplosiveBulletPuff", FBF_USEAMMO | FBF_NORANDOM) // range: 3072
		GATG B 3 A_Light1
		
		NULL A 0 A_Light2
		NULL AA 0 BRIGHT A_SpawnItemEx("RifleCasing", 2, 8, ACS_ExecuteWithResult(aow_casingheight, 32), frandom(1, 3), frandom(0, 1), frandom(1, 3), frandom(45, 60), 128)
		GATF B 2 BRIGHT A_FireBullets(2.6, 1.6, -1, 20, "ExplosiveBulletPuff", FBF_USEAMMO | FBF_NORANDOM) // range: 3072
		GATG A 3 A_Light1
		Goto FireEnd
	Spawn:
		GATG X -1
		Stop
	}
}

Actor MachineGunAmmo : Ammo
{
	Inventory.Amount 30
	Inventory.MaxAmount 400
}