Actor PlasmaGun : AOWWeapon replaces PlasmaRifle 21087
{
    Tag "Plasma Rifle"
	Inventory.PickupMessage "Plasma Rifle"
	Obituary "%o was incinerated by %k's Plasma Rifle."
	Weapon.PreferredSkin "MarinePlasma"

	Weapon.Kickback 25
	
	Weapon.AmmoUse 1
	Weapon.AmmoGive 25
	Weapon.AmmoType "PlasmaRifleAmmo"
	Weapon.AmmoType2 "PlasmaRifleHeat"
	
	Weapon.UpSound "PLRFUP"
	
	States
	{
	Ready: 
		NULL A 0 A_JumpIfInventory("IsRed", 1, "ReadyRed")
	ReadyBlue:
		ENBG A 3 A_WeaponReady
		NULL A 0 A_TakeInventory("PlasmaRifleHeat", 2)
		ENBG A 3 A_WeaponReady
		NULL A 0 A_TakeInventory("PlasmaRifleHeat", 2)
		ENBG A 3 A_WeaponReady
		NULL A 0 A_TakeInventory("PlasmaRifleHeat", 2)
		ENBG A 3 A_WeaponReady
		NULL A 0 A_TakeInventory("PlasmaRifleHeat", 2)
		NULL A 0 A_TakeInventory("PlasmaOverheatWarning", 1)
		Loop
	ReadyRed:
		ENRG A 3 A_WeaponReady
		NULL A 0 A_TakeInventory("PlasmaRifleHeat", 2)
		ENRG A 3 A_WeaponReady
		NULL A 0 A_TakeInventory("PlasmaRifleHeat", 2)
		ENRG A 3 A_WeaponReady
		NULL A 0 A_TakeInventory("PlasmaRifleHeat", 2)
		ENRG A 3 A_WeaponReady
		NULL A 0 A_TakeInventory("PlasmaRifleHeat", 2)
		NULL A 0 A_TakeInventory("PlasmaOverheatWarning", 1)
		Loop
	Deselect:
		NULL A 0 A_TakeInventory("PlasmaOverheatWarning", 1)
		NULL A 0 A_JumpIfInventory("IsRed", 1, "DeselectRed")
	DeselectBlue:
		ENBG A 1 A_Lower
		Loop
	DeselectRed:
		ENRG A 1 A_Lower 
		Loop
	Select:
		NULL A 0 A_JumpIfInventory("IsRed", 1, "SelectRed")
	SelectBlue:
		ENBG A 1 A_Raise
		Loop
	SelectRed:
		ENRG A 1 A_Raise 
		Loop
	Fire:
		NULL A 0 A_JumpIfInventory("PlasmaRifleHeat", 100, "Overheat")
		NULL A 0 A_JumpIfInventory("IsRed", 1, "FireRed")
	FireBlue:
		ENBG A 1 A_GunFlash
		NULL A 0 A_PlaySound("PLRFFIR", CHAN_WEAPON)
		ENBG A 1 offset(1, 35) A_FireCustomMissile("PlasmaShotBlue", 0, true, 2, 0)
		ENBG A 1 offset(0, 37) A_GiveInventory("PlasmaRifleHeat", 2)
		NULL A 0 A_JumpIfInventory("PlasmaRifleHeat", 75, "Warning")
		NULL A 0 A_Refire
		Goto Ready
	FireRed:
		ENRG A 1 A_GunFlash
		NULL A 0 A_PlaySound("PLRFFIR", CHAN_WEAPON)
		ENRG A 1 offset(1, 35) A_FireCustomMissile("PlasmaShotRed", 0, true, 2, 0)
		ENRG A 1 offset(0, 37) A_GiveInventory("PlasmaRifleHeat", 2)
		NULL A 0 A_JumpIfInventory("PlasmaRifleHeat", 75, "Warning")
		NULL A 0 A_Refire
		Goto Ready
	Warning:
		NULL A 0 A_JumpIfInventory("PlasmaOverheatWarning", 1, "Ready")
		NULL A 0 A_PlaySound("PLRFWARN", CHAN_7)
		NULL A 0 A_GiveInventory("PlasmaOverheatWarning", 1)
		Goto Ready
	Overheat:
		NULL A 0 A_TakeInventory("PlasmaOverheatWarning", 1)
		NULL A 0 A_JumpIfInventory("IsRed", 1, "OverheatRed")
	OverheatBlue:
		ENBG A 2 A_PlaySound("PLRFFAIL", CHAN_5)
		NULL A 0 A_PlaySound("PLRFVAPO", CHAN_6)
		ENBG AAAAAA 2 A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 6, -2)
		ENBG AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 3 A_TakeInventory("PlasmaRifleHeat", 2)
		Goto Ready
	OverheatRed:
		ENRG A 2 A_PlaySound("PLRFFAIL", CHAN_5)
		NULL A 0 A_PlaySound("PLRFVAPO", CHAN_6)
		ENRG AAAAAA 2 A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 6, -2)
		ENRG AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 3 A_TakeInventory("PlasmaRifleHeat", 2)
		Goto Ready
	Flash: 
		NULL A 0 A_JumpIfInventory("IsRed", 1, "FlashRed")
	FlashBlue:
		ENRF A 1 BRIGHT A_Light1
		ENRF B 2 BRIGHT A_Light2
		ENRF C 1 BRIGHT A_Light1
		NULL A 0 A_Light0
		Stop
	FlashRed:
		ENRR A 1 BRIGHT A_Light1
		ENRR B 2 BRIGHT A_Light2
		ENRR C 1 BRIGHT A_Light1
		NULL A 0 A_Light0
		Stop
	Spawn:
		PLAS A -1
		Stop
	}
}

Actor PlasmaRifleAmmo : Ammo
{
	Inventory.Amount 40
	Inventory.MaxAmount 300
}

Actor PlasmaRifleHeat : Ammo
{
	Inventory.Amount 0
	Inventory.MaxAmount 100
}

Actor PlasmaOverheatWarning : Boolean {}

Actor PlasmaShot
{
	PROJECTILE
	-RANDOMIZE
	+FORCEXYBILLBOARD
	
	Radius 12
	Height 8
	Speed 28
	Damage (15)
	DamageType "Laser"
	
	RenderStyle Add
	Alpha 0.75
	Scale 0.8
	DeathSound "weapons/plasmax"
	
	Decal PulseScorch
	
	States
	{
	Spawn:
		PLSS AB 4 BRIGHT
		Loop
	Death:
		NULL A 0 A_SpawnItemEx("BaseSmoke", 0, 0, 0,  0, 0, 1)
		PLSE ABCDE 2 BRIGHT 
		Stop
	}
}

Actor PlasmaShotBlue : PlasmaShot
{

}

Actor PlasmaShotRed : PlasmaShot
{
	Translation "192:207=169:191", "240:247=[63,0,0]:[0,0,0]" 
}
