Actor FragGrenadeAmmo : Ammo 
{
	Inventory.PickupMessage "Frag Grenade"
	Inventory.Amount 1
	Inventory.MaxAmount 1
	
	Scale 0.25
	
	States 
	{
	Spawn:
		FRGP A -1
		stop
	}
}

Actor ThrownFragGrenade
{
	Radius 5
	Height 5
	Speed 30
	Damage (0)
	DamageType "Explosion"
	Gravity 0.4
	BounceFactor 0.4
	WallBounceFactor 0.5	
	
	Obituary "%o didn't see %k's frag grenade."
	
    PROJECTILE
    -NOGRAVITY
    +HEXENBOUNCE
    +NOEXTREMEDEATH
    +FORCERADIUSDMG
    +CANBOUNCEWATER
    +BOUNCEONACTORS
    +ROCKETTRAIL
    +FORCEXYBILLBOARD
	
	SeeSound "FRAGBNC"	
	Scale 0.25 
	
	States 
	{
	Spawn:
		FRGX ABCDEFGHABB 5
	Death:
		NULL A 0 A_PlaySound("weapons/rocklx", CHAN_WEAPON)	
		NULL A 0 A_Explode(190, 256)
		NULL AAAAAAAA 0 A_CustomMissile("FragGrenadeShrapnel", 0, 0, frandom(0,360), CMF_AIMDIRECTION | CMF_TRACKOWNER, frandom(2,80))
        NULL A 0 A_SpawnItemEx("Explosion")		
		Stop
	}
}

Actor FragGrenadeShrapnel 
{
	Radius 10
	Height 8
	Damage (4)
	DamageType "Rifle"
	Speed 60
	Gravity 0.6
	BounceCount 10
	
	Obituary "%o was shredded by %k's shrapnel."
	
	PROJECTILE
	+RIPPER
	+BLOODSPLATTER
	+HEXENBOUNCE
	
	Scale 0.3
	
	States 
	{
	Spawn:
		SHRA A 5 BRIGHT
		SHRA A 4 A_Gravity
		SHRA A 55
	Death:
		SHRA A 4 A_FadeOut(0.2)
		Loop
	}
}
