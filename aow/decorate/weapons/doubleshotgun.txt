Actor DoubleShotgunSpawner replaces SuperShotgun
{
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItem("DoubleShotgun")
		stop
	}
}

Actor DoubleShotgun : AOWWeapon 21086
{
	Tag "Double Shotgun"
	Inventory.PickupMessage "Double Shotgun"
	Obituary "%o was splattered by %k's double shotgun."
	Weapon.PreferredSkin "MarineSSG"

	Inventory.RespawnTics 2100

	Weapon.Kickback 30
	
	Weapon.AmmoUse 2
	Weapon.AmmoGive 16
	Weapon.AmmoType "ShotgunAmmo"
	DropItem "Boomstick"
	
	Weapon.UpSound "SSGUP"
	AttackSound "weapons/sshotf"
	
	States
	{
	Ready:
		SHT2 A 1 A_WeaponReady
		Loop
	Deselect:
		SHT2 A 1 A_Lower
		Loop
	Select:
		SHT2 A 1 A_Raise
		Loop
	Fire:
		SHT2 A 3
		NULL A 0 A_GunFlash
		NULL A 0 ACS_NamedExecuteWithResult("aow_traceroffset", 0, 0)
		SHT2 A 7 BRIGHT A_FireBullets(15.7, 11.7, 20, 10, "BulletPuff", FBF_USEAMMO | FBF_NORANDOM, 512)
		Goto Reload
	Reload:
		SHT2 B 7
		SHT2 C 7 A_CheckReload
		NULL AA 0 A_SpawnItemEx("ShellCasing", 0, 0, CallACS("aow_casingheight", 32), frandom(1, 3), frandom(0, 1), 0, frandom(45, 60))
		SHT2 D 7 A_OpenShotgun2
		SHT2 E 7
		SHT2 F 7 A_LoadShotgun2
		SHT2 G 6
		SHT2 H 6 A_CloseShotgun2
		SHT2 A 5 A_ReFire
		Goto Ready
	Flash:
		SHT2 I 3 BRIGHT A_Light1
		SHT2 J 4 BRIGHT A_Light2
		TNT1 A 28 A_Light0
		Stop
	Spawn:
		SGN2 A -1
		Stop
	}
}
