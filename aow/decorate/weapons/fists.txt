Actor FistSwitch : Boolean {}

Actor Unarmed : AOWWeapon
{
	Tag "Unarmed"
	Inventory.PickupMessage "Unarmed"
	Obituary "%k knocked %o out hard."
	Weapon.PreferredSkin "MarineFist"

	+WEAPON.WIMPY_WEAPON
	+WEAPON.MELEEWEAPON
	
	Weapon.Kickback 165
	
	Weapon.UpSound "FISTUP"

	States
	{
	Ready:
		NULL A 0 A_JumpIfInventory("FragGrenadeAmmo", 1, 1)
		Goto RealReady
		NULL A 0 A_JumpIfInventory("AOWGrenade", 1, "FragGrenade")
	RealReady:
		PUNG A 1 A_WeaponReady
		Goto Ready
	Deselect:
		NULL A 0 A_Lower
		PUNG A 1 A_Lower
		Loop
	Select:
		NULL A 0 A_Raise
		PUNG A 1 A_Raise
		Loop
	Fire:
		PUNG B 2 A_JumpIfInventory("FistSwitch", 1, "Swing2")
	Swing1:
		PUNG C 2
		NULL A 0 A_GiveInventory("FistSwitch", 1)
		PUNG D 5 A_CustomPunch(35, true, 0, "FistPuff", 64)
		PUNG C 2
		PUNG B 5 A_ReFire
		Goto Ready
	Swing2:
		PUNG E 2
		PUNG F 2
		NULL A 0 A_TakeInventory("FistSwitch", 1)
		PUNG G 5 A_CustomPunch(35, true, 0, "FistPuff", 64)
		PUNG F 2
		PUNG E 5 A_ReFire
		Goto Ready
	FragGrenade:
		PUNG A 1 offset (0,34)
		PUNG A 1 offset (0,49)
		PUNG A 1 offset (0,79)
		PUNG A 1 offset (0,125)
        NULL A 0 offset (0,33)
		NULL A 0 A_GunFlash("Generic_FragGrenade")
		TNT1 A 51
	    PUNG A 0 A_PlaySound("FISTUP", CHAN_WEAPON)
		PUNG A 1 offset (0,139)
		PUNG A 1 offset (0,112)
		PUNG A 1 offset (0,89)
		PUNG A 1 offset (0,70)
		PUNG A 1 offset (0,55)
		PUNG A 1 offset (0,44)
		PUNG A 1 offset (0,37)
		PUNG A 1 offset (0,0)
		Goto Ready
	}
}
