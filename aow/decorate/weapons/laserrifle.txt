Actor LaserRifle : AOWWeapon
{
	Tag "Laser Rifle"
	Inventory.PickupMessage "Laser Rifle"
	Obituary "%o was burned by %k's laser rifle."
	Weapon.PreferredSkin "MarineGrenade"

	+NOEXTREMEDEATH
	
	Weapon.Kickback 100
	
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0
	Weapon.AmmoType "LaserRifleAmmo"
	
	Weapon.UpSound "LSRFUP"

	States
	{
	Ready:
		NULL A 0 A_JumpIfInventory("IsRed", 1, "ReadyRed")
	ReadyBlue:
		LSRB A 1 A_WeaponReady
		Loop
	ReadyRed:
		LSRF A 1 A_WeaponReady
		Loop
	Deselect:
		NULL A 0 A_JumpIfInventory("IsRed", 1, "DeselectRed")
	DeselectBlue:
		LSRB A 1 A_Lower
		Wait
	DeselectRed:
		LSRF A 1 A_Lower
		Wait
	Select:
		NULL A 0 A_JumpIfInventory("IsRed", 1, "SelectRed")
	SelectBlue:
		LSRB A 1 A_Raise
		Wait
	SelectRed:
		LSRF A 1 A_Raise
		Wait
	Fire:
		NULL A 0 A_PlaySound("LSRFFIRE", CHAN_WEAPON)
		NULL A 0 A_JumpIfInventory("IsRed", 1, "FireRed")
	FireBlue:
		LSRB B 1 BRIGHT offset(0, 34) A_Light2
		NULL A 0 offset(0, 34) A_FireBullets(0, 0, 1, 0, "LaserPuffBlue", FBF_NORANDOM)
		LSRB B 1 BRIGHT offset(0, 36) A_RailAttack(16, 0, true, none, Blue, RGF_SILENT | RGF_FULLBRIGHT, 0, "LaserPuffBlue")
		LSRB C 2 BRIGHT offset(0, 34)
		LSRB A 5 offset(0, 32) A_Light0
		NULL A 0 A_Refire
		LSRB D 3 offset(3, 34)
		LSRB D 3 offset(1, 31)
		LSRB E 2 offset(-2, 28)
		LSRB E 2 offset(-6, 27)
		LSRB FFFF 2 A_FireCustomMissile("OverheatSmokeSpawner", 0, false)
		LSRB F 1 offset(-3, 30)
		LSRB F 1 offset(-1, 33)
		LSRB F 1 offset(2, 37)
		LSRB F 1 offset(5, 42)
		LSRB F 1 offset(7, 43)
		LSRB F 1 offset(9, 44)
		Goto Ready
	FireRed:
		LSRF B 1 BRIGHT offset(0, 34) A_Light2
		NULL A 0 offset(0, 34) A_FireBullets(0, 0, -1, 0, "LaserPuffRed", FBF_NORANDOM)
		LSRF B 1 BRIGHT offset(0, 36) A_RailAttack(16, 0, true, none, Red, RGF_SILENT | RGF_FULLBRIGHT, 0, "LaserPuffRed")
		LSRF C 2 BRIGHT offset(0, 34)
		LSRF A 5 offset(0, 32) A_Light0
		NULL A 0 A_Refire
		LSRF D 3 offset(3, 34)
		LSRF D 3 offset(1, 31)
		LSRF E 2 offset(-2, 28)
		LSRF E 2 offset(-6, 27)
		LSRF FFFF 2 A_FireCustomMissile("OverheatSmokeSpawner", 0, false)
		LSRF F 1 offset(-3, 30)
		LSRF F 1 offset(-1, 33)
		LSRF F 1 offset(2, 37)
		LSRF F 1 offset(5, 42)
		LSRF F 1 offset(7, 43)
		LSRF F 1 offset(9, 44)
		Goto Ready
	Spawn:
		LSRF X -1
		Stop
	}
}

Actor LaserRifleAmmo : Ammo
{
	Inventory.Amount 20
	Inventory.MaxAmount 200
}

Actor LaserPuff : AOWBulletPuff
{
	-ALLOWPARTICLES
	+PUFFONACTORS
	
	DamageType "Laser"
	
	RenderStyle Add
	Scale 0.5
	Alpha 0.75
	VSpeed 0
	
	States
	{
	Spawn:
	Melee:
	Crash:
		VOLX ABCDEFEDCBA 2 BRIGHT
		Stop
	}
}

Actor LaserPuffRed : LaserPuff
{
	Translation "192:207=169:189", "240:246=45:47", "80:111=169:189"
}

Actor LaserPuffBlue : LaserPuff
{

}
