Actor Rifle : AOWWeapon 21042
{
	Tag "Rifle"
	Inventory.PickupMessage "Rifle"
	Obituary "%o was crippled by %k's rifle."
	Weapon.PreferredSkin "MarineBase"

	+NOEXTREMEDEATH
	+WEAPON.ALT_AMMO_OPTIONAL

	Weapon.Kickback 150
	
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "RifleAmmo"
	
	Weapon.UpSound "RIFLUP"

	States
	{
	Ready:
		RIFL A 1 A_WeaponReady
		Loop
	Deselect:
		RIFL A 1 A_Lower
		Loop
	Select:
		RIFL A 1 A_Raise
		Loop
	Fire:
		RIFL A 2
		NULL A 0 A_Light2
		NULL A 0 A_SpawnItemEx("RifleCasing", 8, 8, ACS_ExecuteWithResult(aow_casingheight, 32), frandom(1, 3), frandom(0, 1), frandom(1, 3), frandom(45, 60))
		NULL A 0 A_PlaySound("RIFLFIRE", CHAN_AUTO)
		NULL A 0 ACS_ExecuteWithResult(aow_traceroffset, 0, 0)
		NULL A 0 A_FireBullets(0, 0, 0, 0, "BulletPuff", 0)
		RIFL B 2 BRIGHT A_RailAttack(25, 0, true, none, none, RGF_SILENT, 0, "BulletPuff")
		RIFL C 1 BRIGHT offset(0, 36) A_Light1
		RIFL C 1 BRIGHT offset(0, 40)
		RIFL B 1 BRIGHT offset(0, 42) A_Light0
		RIFL A 1 offset(0, 44)
		RIFL A 1 offset(0, 40)
		RIFL A 1 offset(0, 36)
		RIFL A 8 offset(0, 32)
		RIFL A 4 A_ReFire
		Goto Ready
	Spawn:
		RIFL X -1
		Stop
	}
}

Actor RifleAmmo : Ammo
{
	Inventory.Amount 8
	Inventory.MaxAmount 50
}
