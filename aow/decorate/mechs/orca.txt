Actor Orca : Mech
{
	Radius 58
	Height 128
	Mass 1000
	Health 1000
	
	Player.DisplayName "Orca"
	Player.MaxHealth 1000
	Player.ForwardMove 0.75
	Player.SideMove 0.5
	Player.ViewHeight 64
	Player.AttackZOffset 0
	Player.MorphWeapon "OrcaWeapons"
	
	DamageFactor "Falling", 4.0
	
	+FLOATBOB
	+NOGRAVITY
	
	Scale 0.5
	
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 SetPlayerProperty(0, 1, 3)
		NULL A 0 ACS_ExecuteAlways(aow_stealth, 0, 3)
		NULL A 0 ACS_Execute(aow_script315, 0)
		NULL A 0 A_GiveInventory("MechEjectButton", 1)
		NULL A 0 A_GiveInventory("IsMech", 1)
		ORCV A 35 A_PlaySound("jumpjet/Loop", 5, 1.0, 1)
		ORCV AAAA 35 
		ORCV A 35
		Loop
	See:
		NULL A 0 A_PlaySound("orca/Loop", 5, 1.0, 1)
		NULL A 0 A_JumpIf(Health < 300, "SeeDamaged")
		ORCV A 2 A_SpawnItemEx("MechStepDamageOrca", 32, 0, -8, 0, 0, 0, 0, 0, 0)
		NULL A 0 A_SpawnItemEx("OrcaFlame", -38, -48, 32, 0.5*Random(-4, 4), 0, random(-2, -4), random(-6, 6), 128, 0) 
		NULL A 0 A_SpawnItemEx("OrcaFlame", -38, 48, 32, 0.5*Random(-4, 4), 0, random(-2, -4), random(-6, 6), 128, 0) 
		NULL A 0 A_SpawnItemEx("OrcaSmokePuff", -38, -48, 24, 0.5*Random(-4, 4), 0, random(-1, -2), random(-1, 1), 128, 0) 
		NULL A 0 A_SpawnItemEx("OrcaSmokePuff", -38, 48, 24, 0.5*Random(-4, 4), 0, random(-1, -2), random(-1, 1), 128, 0) 
		NULL A 0 A_JumpIf(WaterLevel >= 1, "Drown")
		Goto See+1
	SeeDamaged:
		NULL A 0 A_PlaySound("orca/Loop", 5, 1.0, 1)
		NULL A 0 A_JumpIf(Health >= 300, "See")
		ORCV A 2 A_SpawnItemEx("EjectedMechSmokePuff", random(-8, 8), random(-8, 8), random(64, 96), 0, 0, 3, 0, 0, 0)
		NULL A 0 A_SpawnItemEx("MechStepDamageOrca", 32, 0, -8, 0, 0, 0, 0, 0, 0)
		NULL A 0 A_SpawnItemEx("OrcaFlame", -38, -48, 32, 0.5*Random(-4, 4), 0, random(-2, -4), random(-6, 6), 128, 0) 
		NULL A 0 A_SpawnItemEx("OrcaFlame", -38, 48, 32, 0.5*Random(-4, 4), 0, random(-2, -4), random(-6, 6), 128, 0) 
		NULL AA 0 A_SpawnItemEx("OrcaSmokePuff", -38, -48, 24, 0.5*Random(-4, 4), 0, random(-1, -2), random(-1, 1), 128, 0) 
		NULL AA 0 A_SpawnItemEx("OrcaSmokePuff", -38, 48, 24, 0.5*Random(-4, 4), 0, random(-1, -2), random(-1, 1), 128, 0) 
		NULL A 0 A_JumpIf(WaterLevel >= 1, "Drown")
		Goto SeeDamaged+1
	Drown:
		NULL A 0 Thing_Damage(0, 100, 12)
		Goto Spawn
	Death:
		NULL A 0
		NULL A 0 A_JumpIfInventory("IsKamikaze", 1, "Death.IonExplosion")
		NULL A 0 A_JumpIfInventory("IsBombCrate", 1, "Death.BombExplosion")
	DeathGo:
		NULL A 0 A_StopSound(5)
		NULL A 0 A_StopSound(CHAN_WEAPON)
		NULL A 0 A_NoBlocking
		TNT1 A 5 A_PlaySoundEx("weapons/rocklx", "SoundSlot5")
		NULL A 0 A_SetTranslucent(0, 0.0)
		NULL A 0 A_SpawnItemEx("EjectedOrca", 0, 0, 0, 5, 0, 0, 0, SXF_NOCHECKPOSITION)
		DISR Z -1
		Stop
	Death.IonExplosion:
		NULL A 0
		NULL A 0 A_JumpIfInventory("IsRed", 1, "Death.NukeExplosion")
		NULL A 0 A_SpawnItemEx("BlueStrikeKamikaze", 0, 0, 32)
		Goto DeathGo
	Death.NukeExplosion:
		NULL A 0
		NULL A 0 A_SpawnItemEx("NukeKamikaze", 0, 0, 32)
		Goto DeathGo
	Death.BombExplosion:
		NULL A 0
		NULL A 0 A_SpawnItemEx("BombExplosion")
		Goto DeathGo
	}
}

Actor OrcaSwitch : PermanentPowerup {Powerup.Type "Orca"}
Actor PowerOrca : AOWMorph {powermorph.playerclass "Orca"}

Actor OrcaWeapons : AOWWeapon
{
	Weapon.SelectionOrder 1
	Weapon.AmmoUse 1
	Weapon.AmmoGive 600
	Weapon.AmmoType "Orca-MinigunAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 60
	Weapon.AmmoType2 "Orca-RocketAmmo"
	Obituary "$OB_ORCA" // "%o was pinned down by %k's Orca."
	+WEAPON.CHEATNOTWEAPON
	+WEAPON.DONTBOB
	States
	{
	Ready:
		MHUD A 1 A_WeaponReady
		Wait
	Deselect:
		NULL A 0 A_JumpIfHealthLower(1, 1)
		Goto Ready
		NULL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait
	Select:
		NULL A 0
		NULL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 A 1 A_Raise
		Wait
	Fire:
		NULL A 0 A_PlayWeaponSound("orca/fire")
		NULL A 0 A_GunFlash
		NULL A 0 A_FireCustomMissile("OrcaFlash", 0, 0, 0, -32)
		NULL A 0 ACS_ExecuteWithResult(aow_traceroffset, 0, 0)
		MHUD B 3 BRIGHT A_FireBullets(2.2, 2.2, -1, 14, "RiflePuff", FBF_USEAMMO | FBF_NORANDOM, 3000)
		MHUD A 2 A_Refire
		Goto Ready
	AltFire:
		NULL A 0 A_PlayWeaponSound("orca/fire2")
		NULL A 0 A_GunFlash
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "AltFireBlue")
		NULL A 0 A_JumpIfInventory("GrenadeSwitch", 1, "AltFire2")
		NULL A 0 A_FireCustomMissile("OrcaFireFlame", 0, 0, -32, -16)
		MHUD B 12 BRIGHT A_FireCustomMissile("OrcaRocket", 0, 1, -32, -16)
		MHUD A 12 BRIGHT A_GiveInventory("GrenadeSwitch", 1)
		MHUD A 2 A_Refire
		Goto Ready
	AltFire2:
		NULL A 0 A_FireCustomMissile("OrcaFireFlame", 0, 0, 32, -16)
		MHUD B 12 BRIGHT A_FireCustomMissile("OrcaRocket", 0, 1, 32, -16)
		MHUD A 12 BRIGHT A_TakeInventory("GrenadeSwitch", 1)
		MHUD A 2 A_Refire
		Goto Ready
	AltFireBlue:
		NULL A 0 A_JumpIfInventory("GrenadeSwitch", 1, "AltFire2Blue")
		NULL A 0 A_FireCustomMissile("OrcaFireFlame", 0, 0, -32, -16)
		MHUD B 12 BRIGHT A_FireCustomMissile("OrcaRocketBlue", 0, 1, -32, -16)
		MHUD A 12 BRIGHT A_GiveInventory("GrenadeSwitch", 1)
		MHUD A 2 A_Refire
		Goto Ready
	AltFire2Blue:
		NULL A 0 A_FireCustomMissile("OrcaFireFlame", 0, 0, 32, -16)
		MHUD B 12 BRIGHT A_FireCustomMissile("OrcaRocketBlue", 0, 1, 32, -16)
		MHUD A 12 BRIGHT A_TakeInventory("GrenadeSwitch", 1)
		MHUD A 2 A_Refire
		Goto Ready
	Flash:
		TNT1 A 1 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		NULL A 0 
		Stop
	}
}

Actor Orca-RocketAmmo : Ammo
{
	Inventory.amount 4
	Inventory.MaxAmount 60
	Ammo.BackpackAmount 4
	Ammo.BackpackMaxAmount 60
}

Actor Orca-MinigunAmmo : Ammo
{
	Inventory.amount 50
	Inventory.MaxAmount 600
	Ammo.BackpackAmount 50
	Ammo.BackpackMaxAmount 600
}

Actor OrcaRocket
{
	PROJECTILE
	-RANDOMIZE
	+ROCKETTRAIL
	Radius 11
	Height 8
	Obituary "$OB_ORCA"
	Speed 40
	Damage (60)
	DeathSound "weapons/rocklx"
	DamageType "Explosion"
	States
	{
	Spawn:
		MISL A 2 BRIGHT
		MISL A -1 BRIGHT A_PlaySoundEx("ambient/rocket", "Body", 1, -1)
		Loop
	Death:
		NULL A 0 A_PlaySoundEx("blanksnd", "Body", 0)
		NULL A 0 A_Explode(80, 128)
		NULL A 0 A_SpawnItemEx("FlameDecay", random(-16, 16), random(-16, 16), 4, 0, 0, 0, 0, 0, 160)
		TNT1 A 5 A_SpawnItemEx("ExplosionMain")
		Stop
	}
}


Actor OrcaRocketBlue : OrcaRocket
{
	DamageType "Explosion"
	States
	{
	Death:
		NULL A 0 A_PlaySoundEx("blanksnd", "Body", 0)
		NULL A 0 A_Explode(80, 128)
		NULL A 0 A_SpawnItemEx("FlameDecayBlue", random(-16, 16), random(-16, 16), 4, 0, 0, 0, 0, 0, 160)
		TNT1 A 5 A_SpawnItemEx("ExplosionMain")
		Stop
	}
}

Actor OrcaFlame
{
	PROJECTILE
	+NOINTERACTION
	+CLIENTSIDEONLY
	Radius 13
	Height 8
	Speed 20
	Damage 0
	RenderStyle Add
	Alpha 1.0
	Scale 0.82
	States
	{
	Spawn:
		NULL A 0
		FLMX ABCDEFGH 1 BRIGHT A_Fadeout(0.125)
		Stop
	}
}

Actor OrcaFireFlame : OrcaFlame
{
	Scale 0.41
	States
	{
	Spawn:
		NULL AA 0 A_SpawnItemEx("OrcaSmokePuff", 0, 0, 0, 0, 0, 1, 0, 128, 0)
		Goto Super::Spawn
	}
}

Actor OrcaFlash : OrcaFlame
{
	Scale 0.205
	States
	{
	Spawn:
		NULL A 0
		FLMX ABCD 1 BRIGHT A_Fadeout(0.25)
		Stop
	}
}

Actor OrcaSmokePuff : PulseBallSmokePuff
{
	Alpha 0.25
	Scale 2.0
}

Actor EjectedOrcaSpawner
{
	+NOINTERACTION
	+SERVERSIDEONLY
	States
	{
	Spawn:
		NULL AA 0 A_SpawnItemEx("EjectedOrca", 0, 0, 0, 4)
		Stop
	}
}

Actor EjectedOrca
{
	PROJECTILE
	-NOGRAVITY
	+NOCLIP
	Radius 58
	Height 128
	Damage (300)
	Scale 0.5
	Speed 10
	Obituary "%o was hit by %k's Orca."
	Translation "112:127=[255,255,255]:[0,0,0]"
	Gravity 0.2
	DamageType "Monster"
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_NoBlocking
		NULL A 0 A_PlaySound("weapons/rocklx", 5)
		ORCV A 10 A_SpawnItemEx("ExplosionMain")
		NULL A 0 A_PlaySound("weapons/rocklx", 6)
		ORCV A 10 A_SpawnItemEx("ExplosionMain")
		NULL A 0 A_ChangeFlag("NOCLIP", 0)
		Goto Spawn+3
	Death:
		NULL A 0 A_StopSound(5)
		NULL A 0 A_StopSound(6)
		NULL A 0 A_SpawnItemEx("MechExplosionSmall", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		Stop
	}
}

Actor OrcaAmmoPackage : CustomInventory
{
	+AUTOACTIVATE
	Inventory.MaxAmount 0
	States
	{
	Pickup:
		NULL A 0 A_GiveInventory("Orca-MinigunAmmo", 600)
		NULL A 0 A_GiveInventory("Orca-RocketAmmo", 60)
		Stop
	}
}
