Actor Wolverine : Mech
{
	Radius 58
	Height 128
	Mass 4000
	Health 3000
	
	Player.DisplayName "Wolverine"
	Player.ForwardMove 0.45
	Player.SideMove 0.15
	Player.MaxHealth 3000
	Player.ViewHeight 96
	Player.AttackZOffset 13
	Player.MorphWeapon "WolverineWeapons"
	
	Scale 1.75
	
	States
	{
	Spawn:
		ROB1 A 35
		ROB1 AAAA 35 A_PlaySound("MECHIDLE", CHAN_5)
		ROB1 A 35
		Loop
	See:
		ROB1 CD 5 A_JumpIfHealthLower(500, "SeeDamaged")
		NULL A 0 A_PlaySound("MECHIDLE", CHAN_5)
		NULL A 0 Radius_Quake(2, 8, 0, 2, 0)
		ROB1 E 5 A_SpawnItemEx("SmallMechStep", 0, -24, 4)
		ROB1 DC 5
		NULL A 0 Radius_Quake(2, 8, 0, 2, 0)
		ROB1 B 5 A_SpawnItemEx("SmallMechStep", 0, 24, 4)
		Goto Spawn
	SeeDamaged:
		ROB1 CD 5 
		NULL A 0 A_PlaySound("MECHIDLE", CHAN_5)
		NULL A 0 Radius_Quake(2, 8, 0, 2, 0)
		NULL A 0 A_SpawnItemEx("SmallMechStep", 0, -24, 4)
		ROB1 E 5 A_SpawnItemEx("DamagedMechSmoke", frandom(-32, 32), frandom(-32, 32), frandom(96, 128), 0, 0, 3)
		ROB1 DC 5
		NULL A 0 Radius_Quake(2, 8, 0, 2, 0)

		NULL A 0 A_SpawnItemEx("SmallMechStep", 0, 24, 4)
		ROB1 B 5 A_SpawnItemEx("DamagedMechSmoke", frandom(-32, 32), frandom(-32, 32), frandom(96, 128), 0, 0, 3)
		Goto Spawn
	Death:
		ROB1 A 10 A_NoBlocking
		NULL A 0 A_StopSound(5)
		NULL A 0 A_StopSound(CHAN_WEAPON)
		ROB1 A 10 A_SpawnItemEx("SmallMechExplosion", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		DISR Z -1 A_SetTranslucent(0, 0.0)
		Stop
	}
}

Actor WolverineWeapons : MechWeapons
{
	Weapon.SelectionOrder 1
	Obituary "%o was overwhelmed by %k's Wolverine."
	+WEAPON.CHEATNOTWEAPON
	+WEAPON.DONTBOB
	Weapon.PreferredSkin "WolverineMech"
	States
	{
	Ready:
		MHUD A 1 A_WeaponReady
		Loop
	Fire:
		NULL A 0 A_JumpIfInventory("Wolverine-WhatWeapon", 2, "FireMinigun")
		NULL A 0 A_JumpIfInventory("Wolverine-WhatWeapon", 1, "FirePulse")
		NULL A 0 A_JumpIfInventory("Reload-LRM", 1, "FirePulse")
		MHUD A 10 A_JumpIfInventory("MechRocketAmmo", 10, 1)
		Goto AltFire
		MHUD A 2 A_PlayWeaponSound("LRMFIRE")
		NULL A 0 Radius_Quake(2, 35, 0, 1, 0)
		NULL A 0 A_TakeInventory("MechRocketAmmo", 10)
		NULL A 0 ACS_ExecuteAlways(aow_script319, 0, 1, 4) // reload
		NULL A 0 A_GunFlash
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "FireBlue")
		MHUD BBBBBBBBBB 2 A_FireCustomMissile("LongRangeMissile", 0, 0, 25)
		MHUD A 20
		MHUD A 2 A_Refire
		Goto Ready
	FireBlue:
		MHUD BBBBBBBBBB 2 A_FireCustomMissile("LongRangeMissileBlue", 0, 0, 25)
		MHUD A 20
		MHUD A 2 A_Refire
		Goto Ready
	FirePulse:
		NULL A 0
		NULL A 0 Radius_Quake(1, 4, 0, 1, 0)
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "FirePulseBlue")
		NULL A 0 A_FireCustomMissile("PulseBall", 0, 1, 25)
		MHUD D 2 BRIGHT A_GunFlash
		MHUD C 1
		MHUD C 2 A_Refire
		Goto Ready
	FirePulseBlue:
		NULL A 0 A_FireCustomMissile("PulseBallBlue", 0, 1, 25)
		MHUD D 2 BRIGHT A_GunFlash
		MHUD C 1
		MHUD C 2 A_Refire
		Goto Ready
	FireMinigun:
		MHUD A 10 A_JumpIfInventory("Wolverine-MinigunAmmo", 2, 2)
		NULL A 0 A_PlaySoundEx("minigun/spin", "Weapon", 0)
		Goto AltFire
		NULL A 0 Radius_Quake(2, 8, 0, 1, 0)
		NULL A 0 A_PlaySoundEx("minigun/fire", "Weapon", 1)
		NULL A 0 A_GunFlash
		NULL A 0 A_TakeInventory("Wolverine-MinigunAmmo", 1)
		NULL A 0 A_SpawnItemEx("RifleShellCasing", 2, 8, 30, frandom(-3, -1), frandom(0, 1), frandom(1, 3), frandom(-60, -45), 128)
		NULL A 0 ACS_ExecuteWithResult(aow_traceroffset, 0, 0)
		MHUD B 2 BRIGHT A_FireBullets(3.4, 3.4, -1, 20, "RiflePuff", FBF_USEAMMO | FBF_NORANDOM)
		MHUD A 1
		NULL A 0 A_GunFlash
		NULL A 0 A_TakeInventory("Wolverine-MinigunAmmo", 1)
		NULL A 0 A_SpawnItemEx("RifleShellCasing", 2, 8, 30, frandom(-3, -1), frandom(0, 1), frandom(1, 3), frandom(-60, -45), 128)
		NULL A 0 ACS_ExecuteWithResult(aow_traceroffset, 0, 0)
		MHUD B 2 BRIGHT A_FireBullets(3.4, 3.4, -1, 20, "RiflePuff", FBF_USEAMMO | FBF_NORANDOM)
		MHUD A 1
		NULL A 0 A_ReFire
		NULL A 0 A_PlaySoundEx("minigun/spin", "Weapon", 0)
		MHUD A 4
		Goto Ready
	Flash:
		TNT1 A 1 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	AltFire:
		NULL A 0
		MHUD C 35 ACS_ExecuteAlways(aow_script314, 0)
		MHUD A 18
		Goto Ready
	}
}