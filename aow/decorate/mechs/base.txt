// Actors common to all mechs
Actor Mech : PlayerPawn
{
	DamageFactor "Fist", 0.35
	DamageFactor "Bullet", 0.8
	DamageFactor "Rifle", 0.8
	DamageFactor "Explosion", 1.25
	DamageFactor "Fire", 1.25
	DamageFactor "Chemical", 1.75
	DamageFactor "ChemicalSpecial", 0.0
	DamageFactor "Laser", 1.0
	DamageFactor "RazorWireRed", 1.0
	DamageFactor "RazorWireBlue", 1.0
	DamageFactor "MechExplosion", 2.0
	DamageFactor "TimeGun", 1.0
	DamageFactor "Disarm", 0.0
	DamageFactor "Deconstruction", 0.0
	DamageFactor "Artillery", 0.9
	DamageFactor "MechStomp", 0.0
	DamageFactor "Monster", 1.0
	DamageFactor "Falling", 12.0
	
	MaxStepHeight 40
	
	Player.JumpZ 0
	Player.DamageScreenColor "00 00 00"
	Player.SoundClass "Mech"
	
	PainChance 0
	
	Mass 0x7FFFFFFF
	
	-NOBLOCKMONST
	+NOSKIN
	+NOBLOOD
}

Actor MechSelectedWeapon : Integer {}

Actor MechWeapons : Weapon
{
	+CHEATNOTWEAPON
	+DONTBOB

	States
	{
	Spawn:
		NULL A 0
		Stop
	Flash:
		TNT1 A 1 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	}
}

// Mech explosions
Actor SmallMechExplosion
{
	Obituary "%o was blown up by %k's mech."
	DamageType "Explosion"
	
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_PlaySound("MECHEXP3", 1)
		NULL A 0 A_PlaySound("MECHEXP4", 2)
		NULL A 0 A_PlaySound("MECHEXP3", 3, 0.5)
		NULL A 0 A_PlaySound("MECHEXP4", 4, 0.5)
		NULL A 0 A_Explode(50, 512)
		NULL A 0 A_Explode(350, 128)
		NULL A 0 A_Explode(350, 256)
		NULL AAAAA 0 A_SpawnItemEx("MechFallingJunk", 0, 0, frandom(64, 120),  frandom(-5, 5), frandom(-5, 5), frandom(9, 14),  0, 0, 64)
		TNT1 A 8 A_SpawnItemEx("ExplosionLarge")
		TNT1 A 8 A_SpawnItemEx("ExplosionLarge", 12, 16, 64)
		TNT1 A 8 A_SpawnItemEx("ExplosionLarge", 14, 32, 96)
		TNT1 A 8 A_SpawnItemEx("ExplosionLarge", 26, 48, 128)
		Stop
	}
}

Actor BigMechExplosion
{
	Obituary "%o was blown up by %k's big mech."
	DamageType "Explosion"
	
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_PlaySound("MECHEXP1", 1)
		NULL A 0 A_PlaySound("MECHEXP2", 2)
		NULL A 0 A_PlaySound("MECHEXP1", 3, 0.5)
		NULL A 0 A_PlaySound("MECHEXP2", 4, 0.5)
		NULL A 0 A_Explode(100, 512)
		NULL A 0 A_Explode(700, 128)
		NULL A 0 A_Explode(700, 256)
		NULL AAAAAAAAAA 0 A_SpawnItemEx("MechFallingJunk", 0, 0, frandom(64, 120),  frandom(-5, 5), frandom(-5, 5), frandom(9, 14), 0, 0, 64)
		TNT1 A 8 A_SpawnItemEx("ExplosionLarge")
		TNT1 A 8 A_SpawnItemEx("ExplosionLarge", 12, 16, 64)
		TNT1 A 8 A_SpawnItemEx("ExplosionLarge", 14, 32, 96)
		TNT1 A 8 A_SpawnItemEx("ExplosionLarge", 26, 48, 128)
		TNT1 A 8 A_SpawnItemEx("ExplosionLarge", 38, 72, 160)
		TNT1 A 8 A_SpawnItemEx("ExplosionLarge", 50, 96, 192)
		Stop
	}
}

// Mech weapons
Actor LRMReloading : Boolean {}

Actor LongRangeMissile
{
	Radius 11
	Height 8
	Speed 45
	Damage (30)
	DamageType "Explosion"
	
	PROJECTILE
	-RANDOMIZE
	+ROCKETTRAIL
	
	Scale 0.8
	
	Decal "Scorch"
	DeathSound "weapons/rocklx"
	
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_PlaySound("RCKTFLY", CHAN_BODY, 1.0, true)
		MISL A -1 BRIGHT
		Stop
	Death:
		NULL A 0 A_StopSound(CHAN_BODY)
		NULL A 0 A_Explode(30, 128, 0)
		NULL A 0 A_SpawnItemEx("Explosion")
		TNT1 A 1 // play sounds
		Stop
	}
}

// Mech steps
Actor MechStepSound : Effect
{
	States
	{
	Spawn:
		NULL A 0
		TNT1 A 1 A_PlaySound("MECHSTEP")
		Stop
	}
}

Actor SmallMechStep
{
	Speed 1
	Damage 0
	DamageType "MechStomp"

	PROJECTILE
	+RANDOMIZE
	+NOCLIP
	+NODAMAGETHRUST
	
	Scale 0.7
	
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 Radius_Quake(2, 8, 0, 2, 0)
		NULL A 0 A_SpawnItemEx("SmallMechStepSmoke")
		NULL A 0 A_SpawnItemEx("MechStepSound")
		NULL A 0 A_Explode(72, 128, 0)
		Stop
	}  
}

Actor BigMechStep : SmallMechStep
{
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 Radius_Quake(3, 10, 0, 4, 0)
		NULL A 0 A_SpawnItemEx("BigMechStepSmoke")
		NULL A 0 A_SpawnItemEx("MechStepSound")
		NULL A 0 A_Explode(256, 128, 0)
		Stop
	}  
}

// Mech smoke
Actor SmallMechStepSmoke : BaseSmoke 
{
	Scale 4.0
}

Actor BigMechStepSmoke  : BaseSmoke 
{
	Scale 6.0
}

Actor DamagedMechSmoke : BaseSmoke	
{
	Scale 8.0
}

// Mech fallling junk
Actor MechFallingJunk
{
	Obituary "%o was blown up by %k's junk!"

	Radius 11
	Height 8
	Speed 1
	Damage (15)
	DamageType "Explosion"
	Gravity 0.3
	
	PROJECTILE
	-RANDOMIZE
	-NOGRAVITY
	
	Scale 0.03
	
	States
	{
	Spawn:
		TNT1 A 6
		TNT1 A 2 A_SpawnItemEx("BaseSmoke",  0, 0, 0,  0, 0, 1)
		Wait
	Death:
		NULL A 0 A_PlaySound("MECHJUNK")
		NULL A 0 A_Explode(35, 128, 1)
		NULL A 0 A_SpawnItemEx("Explosion")
		TNT1 A 1 // play sounds
		Stop
	}
}
