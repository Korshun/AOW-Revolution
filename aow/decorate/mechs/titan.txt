Actor Titan : Mech
{
	Radius 58
	Height 128
	Health 5000
	
	Player.DisplayName "Titan"
	Player.ForwardMove 0.35
	Player.SideMove 0.15
	Player.MaxHealth 5000
	Player.ViewHeight 96
	Player.AttackZOffset 13
	Player.MorphWeapon "TitanWeapons"
	
	Scale 1.1
	
	States
	{
	Spawn:
		ROB3 A 35
		ROB3 AAAA 35 A_PlaySound("mech/idle", CHAN_5, true, -1)
		ROB3 A 35
		Loop
	See:
		NULL A 0 A_JumpIfHealthLower(500, "SeeDamaged")
	SeeNormal:
		ROB3 CD 7
		NULL A 0 A_PlaySound("mech/idle", CHAN_5, true, -1)
		ROB3 E 10 A_SpawnItemEx("BigMechStep", 0, -34, 4)
		ROB3 DC 7
		ROB3 B 10 A_SpawnItemEx("BigMechStep", 0, 34, 4)
		Goto Spawn
	SeeDamaged:
		ROB3 CD 7
		NULL A 0 A_PlaySound("mech/idle", CHAN_5, true, -1)
		NULL A 0 A_SpawnItemEx("DamagedMechSmoke", frandom(-32, 32), frandom(-32, 32), frandom(96, 128),  0, 0, 3)
		ROB3 E 10 A_SpawnItemEx("BigMechStep", 0, -34, 4)
		ROB3 DC 7
		NULL A 0 A_SpawnItemEx("DamagedMechSmoke", frandom(-32, 32), frandom(-32, 32), frandom(96, 128),  0, 0, 3)
		ROB3 B 10 A_SpawnItemEx("BigMechStep", 0, 34, 4)
		Goto Spawn
	Death:
		ROB3 A 10 A_NoBlocking
		NULL A 0 A_StopSound(CHAN_5)
		NULL A 0 A_StopSound(CHAN_WEAPON)
		ROB3 A 35 A_SpawnItemEx("BigMechExplosion", 0, 0, 0,  0, 0, 0,  0, SXF_NOCHECKPOSITION)
		DISR Z -1 A_SetTranslucent(0.0, 0.0)
		Stop
	}
}

Actor TitanWeapons : MechWeapons
{
	Obituary "%o was crushed by %k's Titan."

	States
	{
	Ready:
		MHUD C 1 A_WeaponReady
		Loop
	}
}
