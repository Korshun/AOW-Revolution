Actor MadCat : Mech
{
	Health 4000
	Radius 64
	Height 144
	Mass 4000
	MaxStepHeight 40
		
	Player.DisplayName "Mad Cat"
	Player.MaxHealth 4000
	Player.ColorRange 112, 127
	Player.ForwardMove 0.4
	Player.SideMove 0.15
	
	Player.damagescreencolor "00 00 00"
	Player.viewheight 112
	Player.attackzoffset 18
	Player.MorphWeapon "MadCatWeapons"
	
	Scale 0.75
	
	DamageFactor "Falling", 12.0
	
	States
	{
	
	Spawn:
		MADC A 8 A_SpawnItemEx("BigMechStep", 0, -42, 4)
		MADC K 35
		MADC KKKK 35 A_PlaySound("MECHIDLE", CHAN_5)
		MADC K 35
		Loop
	GetIn:
		MAD2 BB 7 SetPlayerProperty(0, true, PROP_FROZEN)
		MAD2 BA 7
		MADC ZYXWV 7
		NULL A 0 SetPlayerProperty(0, false, PROP_FROZEN)
		Goto Spawn+6
	See:
		NULL A 0 A_JumpIfHealthLower(500, "SeeDamaged")
	SeeNormal:
		MADC ABCD 4
		NULL A 0 A_PlaySound("MECHIDLE", CHAN_5, true, -1)
		MADC E 4 A_SpawnItemEx("BigMechStep", 0, -42, 4)
		MADC FGHI 4
		MADC J 4 A_SpawnItemEx("BigMechStep", 0, 42, 4)
		Goto Spawn
	SeeDamaged:
		MADC ABCD 4 
		NULL A 0 A_PlaySound("MECHIDLE", CHAN_5, true, -1)
		NULL A 0 A_SpawnItemEx("DamagedMechSmoke", frandom(-32, 32), frandom(-32, 32), frandom(96, 128), 0, 0, 3, 0, 0, 0)
		MADC E 4 A_SpawnItemEx("BigMechStep", 0, -42, 4)
		MADC FGHI 4
		NULL A 0 A_SpawnItemEx("DamagedMechSmoke", frandom(-32, 32), frandom(-32, 32), frandom(96, 128), 0, 0, 3, 0, 0, 0)
		MADC J 4 A_SpawnItemEx("BigMechStep", 0, 42, 4)
		Goto Spawn
	Death:
		NULL A 0 A_NoBlocking
		MADC LM 4
		MADC NOPQRSTUUU 4 A_SpawnItemEx("DamagedMechSmoke", frandom(-32, 32), frandom(-32, 32), frandom(32, 64), 0, 0, 3)
		TNT1 A 35 A_SpawnItemEx("BigMechExplosion", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		DISR Z -1 A_SetTranslucent(0, 0.0)
		Stop
	}
}

Actor MechReloadShockCannon : Boolean {}

Actor MechShockRifleCharge : Ammo
{
	Inventory.Amount 20
	Inventory.MaxAmount 200
}

Actor MadCatWeapons : MechWeapons
{
	Obituary "%o was destroyed by %k's Mad Cat."

	States
	{
	Ready:
		MHUD A 1 A_WeaponReady
		Loop
	Fire:
		NULL A 0 A_JumpIfInventory("MechSelectedWeapon", 2, "FireMissiles")
		NULL A 0 A_JumpIfInventory("MechSelectedWeapon", 1, "FireShockRifle")
	FireShockCannon:
		NULL A 0 A_PlayWeaponSound("SHCNFIRE")
		NULL A 0 A_GunFlash
		NULL A 0 A_FireCustomMissile("ShockCannonBall", 0, 0, 64, 0)
		NULL A 0 A_FireCustomMissile("ShockCannonBall", 0, 0, -64, 0)
		MHUD D 15 BRIGHT
		MHUD C 35
		MHUD C 2 A_ReFire
		Goto Ready
	FireShockRifle:
		MHUD A 10
		NULL A 0 Radius_Quake(2, 8, 0, 1, 0)
		NULL A 0 A_GunFlash
		NULL A 0 A_PlayWeaponSound("VOLTFIRE")
		NULL A 0 A_TakeInventory("MechShockRifleCharge", 4)
		MHUD B 2 BRIGHT A_FireBullets(6.2, 6.2, 2, 20, "VoltagePuff", FBF_USEAMMO | FBF_NORANDOM)
		MHUD A 1
		NULL A 0 A_GunFlash
		NULL A 0 A_PlayWeaponSound("VOLTFIRE")
		NULL A 0 A_SpawnItemEx("CellCasing", 8, -96, 128,  frandom(1, 3), frandom (-8, -12), frandom(1, 3))
		NULL A 0 A_SpawnItemEx("CellCasing", 8,  96, 128,  frandom(-3, -1), frandom (8, 12), frandom(1, 3))
		MHUD B 2 BRIGHT A_FireBullets(6.2, 6.2, 2, 20, "VoltagePuff", FBF_USEAMMO | FBF_NORANDOM)
		MHUD A 1
		MHUD A 4 A_ReFire
		Goto Ready
	FireMissiles:
		MHUD A 2 A_PlayWeaponSound("LRMFIRE")
		NULL A 0 Radius_Quake(2, 70, 0, 1, 0)
		NULL A 0 A_GunFlash
		MHUD BBBBBBBBBB 2 A_FireCustomMissile("LongRangeMissile", 0, false, frandom(22, 48), frandom(64, 72))
		MHUD A 10
		MHUD A 2 A_PlayWeaponSound("LRMFIRE")
		NULL A 0 A_GunFlash
		MHUD BBBBBBBBBB 2 A_FireCustomMissile("LongRangeMissile", 0, false, frandom(-48, -22), frandom(64, 72))
		MHUD C 20
		MHUD A 2 A_ReFire
		Goto Ready
	}
}

// Shock cannon ball
Actor ShockCannonBall
{
	Radius 8
	Height 8
	Speed 50
	Damage (250)
	DamageType "Laser"
	
	PROJECTILE
	
	RenderStyle Add
	Alpha 0.9
	Scale 1.5
	
	DeathSound "SHCNEXP"
	
	States
	{
	Spawn:
		PPCX A 1 BRIGHT
		NULL A 0 A_SpawnItemEx("ShockCannonBallFX")
		NULL A 0 A_Explode(4, 256)
		Loop
	Death:
		TNT1 A 2 A_CustomMissile("ShockCannonBallImpact")
		TNT1 AAAA 2 BRIGHT A_SpawnItemEx("ShockCannonExplosion2")
		TNT1 AA 2 BRIGHT A_SpawnItemEx("ShockCannonExplosion")
		Stop
	}
}

Actor ShockCannonExplosion
{	
	DamageType "Explosion"

	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_PlaySound("SHCNEXP", CHAN_AUTO)
		NULL A 0 A_Explode(10, 512, 0)
		TNT1 A 1 // play sound
		Stop
	}  
}

Actor ShockCannonExplosion2 : ShockCannonExplosion
{
	DamageType "Laser"
}

// Shock cannon ball effects
Actor ShockCannonBallFX : Effect
{
	States
	{
	Spawn:
		NULL A 0
		NULL AAAA 0 A_SpawnItemEx("VoltageSpark", frandom(-16, 16), frandom(-16, 16), frandom(-16, 16),  frandom(-8, 8), frandom(-8, 8), frandom(-8, 8),  0, 0, 32)
		NULL AAAA 0 A_SpawnItemEx("VoltageSpark", frandom(-32, 0), frandom(-16, 16), frandom(-16, 16),  frandom(-8, 8), frandom(-8, 8), frandom(-8, 8),  0, 0, 32)
		NULL AAAA 0 A_SpawnItemEx("VoltageSpark", frandom(-54, -32), frandom(-16, 16), frandom(-16, 16),  frandom(-8, 8), frandom(-8, 8), frandom(-8, 8),  0, 0, 32)
		NULL AAAA 0 A_SpawnItemEx("VoltageSpark", frandom(-92, -54), frandom(-16, 16), frandom(-16, 16),  frandom(-8, 8), frandom(-8, 8), frandom(-8, 8),  0, 0, 32)
		NULL A 0 A_SpawnItemEx("VoltagePuffNoDecay", frandom(-64, 64), frandom(-64, 64), frandom(-64, 64),  frandom(-8, 8), frandom(-8, 8), frandom(-8, 8))
		Stop
	}
}

Actor VoltagePuffNoDecay : VoltagePuff
{
	States
	{
	Spawn:
		VOLX A 2 BRIGHT
		VOLX BCDEFEDCB 2 BRIGHT
		Stop
	}
}

Actor ShockCannonBallImpact : Effect
{
	RenderStyle Add
	Alpha 0.9
	Scale 3.0
	
	ReactionTime 7
	
	States
	{
	Spawn:
		NULL A 0
		NULL AAAAAA 0 A_SpawnItemEx("ShockCannonTrail", 0, 0, frandom(16, 32),  0, 0, frandom(16, 32))
		NULL AAAAAAAA 0 A_SpawnItemEx("VoltageSpark", frandom(-64, 64), frandom(-64, 64), frandom(-64, 64),  frandom(-8, 8), frandom(-8, 8), frandom(-8, 8))
	Idle:
		PPCX A 2 BRIGHT A_FadeOut(0.05)
		NULL AAAAAAAA 0 A_SpawnItemEx("VoltageSpark", frandom(-64, 64), frandom(-64, 64), frandom(-64, 64),  frandom(-8, 8), frandom(-8, 8), frandom(-8, 8))
		NULL AAA 0 A_SpawnItemEx("VoltagePuff", frandom(-64, 64), frandom(-64, 64), frandom(-64, 64),  frandom(-8, 8), frandom(-8, 8), frandom(-8, 8))
		NULL A 0 A_Countdown
		Loop
	Death:
		PPCX AAAAAA 2 BRIGHT A_FadeOut(0.15)
		Stop
	}
}

Actor ShockCannonTrail : Effect
{
	Radius 1
	Height 1
	Speed 2
	Damage 0
	
	PROJECTILE
	-RANDOMIZE
	
	RenderStyle Add
	Alpha 0.5
	Scale 0.75
	
	States
	{
	Spawn:
		VOLX AAAAAAAAA 2 BRIGHT A_FadeOut(0.07)
		Stop
	Death:
		NULL A 0 A_SpawnItemEx("VoltageDecay", frandom(-16, 16), frandom(-16, 16), frandom(-16, 16),  0, 0, 0,  0, 0, 128)
		Stop
	}
}
